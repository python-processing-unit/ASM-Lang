# Decimal <-> binary helpers
#
# API:
# DEC_CHAR_TO_INT(STR:ch) -> INT
# INT_TO_DEC_CHAR(INT:d) -> STR
# DEC_TO_INT(STR:s) -> INT
# INT_TO_DEC(INT:n) -> STR

INT: DEC_BASE = 1010  # 10

# Fast digit lookup tables.
MAP: DEC_CHAR_MAP = < ^
    "0" = 0, ^
    "1" = 1, ^
    "2" = 10, ^
    "3" = 11, ^
    "4" = 100, ^
    "5" = 101, ^
    "6" = 110, ^
    "7" = 111, ^
    "8" = 1000, ^
    "9" = 1001 ^
>

MAP: DEC_INT_MAP = < ^
    0 = "0", ^
    1 = "1", ^
    10 = "2", ^
    11 = "3", ^
    100 = "4", ^
    101 = "5", ^
    110 = "6", ^
    111 = "7", ^
    1000 = "8", ^
    1001 = "9" ^
>

FUNC DEC_CHAR_TO_INT(STR:ch):INT{
    IF(NOT(KEYIN(ch, DEC_CHAR_MAP))){
        THROW("Invalid decimal character: '", ch, "'")
    }
    RETURN(DEC_CHAR_MAP<ch>)
}

FUNC INT_TO_DEC_CHAR(INT:d):STR{
    IF(NOT(KEYIN(d, DEC_INT_MAP))){
        THROW("Invalid decimal digit: '", INT_TO_DEC(d), "'")
    }
    RETURN(DEC_INT_MAP<d>)
}

FUNC DEC_TO_INT(STR:s):INT{
    INT: len = SLEN(s)
    ASSERT( GT(len, 0) )

    INT: idx = 0
    # skip any leading spaces before the sign or first digit
    WHILE(AND( ^
        LT(idx, len), ^
        EQ(SLICE(s, SUB(len, ADD(idx, 1)), SUB(len, ADD(idx, 1))), " ")) ^
    ){
        idx = ADD(idx, 1)
    }
    ASSERT( LT(idx, len) )

    INT: is_neg = 0
    INT: ridx = SUB(len, ADD(idx, 1))
    STR: first = SLICE(s, ridx, ridx)
    # support optional leading '+' or '-' and allow spaces after the sign
    IF(EQ(first, "-")){
        is_neg = 1
        idx = ADD(idx, 1)
    } ELSE {
        IF(EQ(first, "+")){
            idx = ADD(idx, 1)
        }
    }

    # skip spaces after the optional sign
    WHILE(AND( ^
        LT(idx, len), ^
        EQ(SLICE(s, SUB(len, ADD(idx, 1)), SUB(len, ADD(idx, 1))), " ") ^
    )){
        idx = ADD(idx, 1)
    }
    ASSERT( LT(idx, len) )

    INT: acc = 0
    WHILE( LT(idx, len) ){
        INT: ridx2 = SUB(len, ADD(idx, 1))
        STR: ch = SLICE(s, ridx2, ridx2)
        INT: digit = DEC_CHAR_TO_INT(ch)
        acc = ADD( MUL(acc, DEC_BASE), digit )
        idx = ADD(idx, 1)
    }

    IF(is_neg){
        RETURN( NEG(acc) )
    }
    RETURN(acc)
}

FUNC INT_TO_DEC(INT:n):STR{
    IF(NOT(n)){
        RETURN("0")
    }

    INT: is_neg = 0
    IF(LT(n, 0)){
        is_neg = 1
        n = ABS(n)
    }

    STR: out = ""
    WHILE( GT(n, 0) ){
        INT: digit = MOD(n, DEC_BASE)
        n = DIV(n, DEC_BASE)
        STR: ch = INT_TO_DEC_CHAR(digit)
        out = JOIN(ch, out)  # prepend to maintain most-significant-first order
    }

    IF(is_neg){
        out = JOIN("-", out)
    }
    RETURN(out)
}
